{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejercicio1
procesos
  proceso depositarFlores(E flores: numero)
  comenzar
    repetir flores
      depositarFlor
  fin
  proceso juntarFlores(ES flores: numero; ES esquinaVacia: numero)
  comenzar
    si(HayFlorEnLaEsquina)
      mientras(HayFlorEnLaEsquina)
        tomarFlor
        flores:= flores + 1
    sino
      esquinaVacia:= esquinaVacia + 1
  fin
  proceso recorrerAvenida
  variables
    flores: numero
    esquinaVacia: numero
  comenzar
    flores := 0
    esquinaVacia := 0
    repetir 99
      juntarFlores(flores,esquinaVacia)
      mover
    juntarFlores(flores,esquinaVacia)
    Informar("cantidad de flores juntadas ", flores)
    Informar("cantidad de esquinas vacias ", esquinaVacia)
    depositarFlores(flores)
  fin
areas
  ciudad1: AreaP (1,1,1,100)
  ciudad2: AreaP (3,1,3,100)
  ciudad3: AreaP (5,1,5,100)
robots
  robot robot1
  variables
    avenida: numero
  comenzar
    avenida:= 1
    repetir 2
      recorrerAvenida
      avenida := avenida + 2
      Pos(avenida,1)
    recorrerAvenida
  fin
variables
  R_info: robot1
comenzar
  AsignarArea(R_info, ciudad1)
  AsignarArea(R_info, ciudad2)
  AsignarArea(R_info, ciudad3)
  Iniciar(R_info, 1,1)
fin

